group 'com.cttdy.ssm'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    if (!project.hasProperty("env")) {
        println '没有配置数据环境，默认使用 开发环境'
        env = "dev"

    }
    println "使用数据库环境为:${project['env']}"

    /*asciiDocOutputDir = file("${buildDir}/asciidoc")
    println asciiDocOutputDir.absolutePath
    swaggerOutputDir = file("${buildDir}/swagger")
    println swaggerOutputDir.absolutePath*/
}

war{
    archiveName="${baseName}-${project.version}.${extension}"
}

sourceSets {
    main {
        resources {
            srcDir("src/main/resources")
            if (project['env'] == 'dev') {
                srcDir("src/main/resources-dev")
            } else if (project['env'] == 'prod') {
                srcDir('src/main/resources-prod')
            }
            srcDir("src/main/java")
        }
    }
}


configurations {
    mybatisGenerator
}

def getDbProperties = {
    def properties = new Properties()
    def dbPropertiesPath = sourceSets.main.resources.srcDirs[1].path
    file("$dbPropertiesPath/jdbc.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties
}

task mybatisGenerate << {
    def properties = getDbProperties()
    ant.properties['targetProject'] = projectDir.path
    ant.properties['driverClass'] = properties.getProperty("driver")
    ant.properties['connectionURL'] = properties.getProperty("url")
    ant.properties['userId'] = properties.getProperty("username")
    ant.properties['password'] = properties.getProperty("password")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = this.modelPackage
    ant.properties['mapperPackage'] = this.mapperPackage
    ant.properties['sqlMapperPackage'] = this.sqlMapperPackage
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: 'db/generatorConfig.xml', verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile "jstl:jstl:1.2"
    compile "org.slf4j:slf4j-log4j12:1.7.25"
    compile "com.google.code.gson:gson:2.8.2"
    compile 'com.google.guava:guava:24.0-jre'
    compile "org.apache.tomcat:tomcat-servlet-api:8.0.24"
    providedCompile "javax.servlet:servlet-api:2.5"
    providedCompile "javax.servlet.jsp:jsp-api:2.2"

    compile "org.springframework:spring-beans:$springVersion"
    compile "org.springframework:spring-web:$springVersion"
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.springframework:spring-tx:$springVersion"
    compile "org.springframework:spring-jdbc:$springVersion"
    compile "org.springframework:spring-test:$springVersion"

    compile "org.springframework:spring-websocket:$springVersion"
    compile "org.springframework:spring-messaging:$springVersion"

    compile "org.aspectj:aspectjweaver:1.8.10"
    compile "com.fasterxml.jackson.core:jackson-core:2.9.0"
    compile "com.fasterxml.jackson.core:jackson-annotations:2.9.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.0"
    compile "commons-fileupload:commons-fileupload:1.3.3"
    //compile "com.google.code.gson:gson:2.8.2"

    compile "com.alibaba:druid:1.1.3"
    compile "commons-dbcp:commons-dbcp:1.4"
    compile "mysql:mysql-connector-java:6.0.6"

    compile "org.mybatis:mybatis-spring:1.3.1"
    compile "org.mybatis:mybatis:3.4.5"

    mybatisGenerator "org.mybatis.generator:mybatis-generator-core:1.3.5"
    mybatisGenerator "mysql:mysql-connector-java:5.1.44"
    mybatisGenerator "tk.mybatis:mapper:3.4.0"
}

